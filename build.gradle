apply from: "${rootProject.rootDir}/common_config.gradle"    //引用额外的配置
if (project.ext.isSoulLoginDebug) {
    apply plugin: 'com.android.application'  //组件开发模式，独立运行
} else {
    apply plugin: 'com.android.library'  //集成开发模式
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'  //kapt3插件

android {
    project.ext.setDefaultConfig android    //调用配置信息

    defaultConfig {
        kapt {  //ARouter Config
            arguments {
                arg("AROUTER_MODULE_NAME", project.getName())
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            if (project.ext.isSoulLoginDebug) {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                resources {  //排除module下的所有文件
                    exclude 'src/main/module/*'
                }
            }
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "com.android.support:appcompat-v7:${project.ext.supportVersion}"
    implementation "com.android.support:design:${project.ext.supportVersion}"
    implementation "com.android.support:support-annotations:${project.ext.supportVersion}"
    implementation "com.android.support.constraint:constraint-layout:${project.ext.constraintLayoutVersion}"
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"


    implementation project(path: ':kotlin_baseLib')
    kapt 'com.alibaba:arouter-compiler:1.2.1'
}
